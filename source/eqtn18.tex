\def\Date{2 Jun 2024}

\documentclass[12pt]{cdblatex}
\usepackage{eqtns}
\usepackage{headfoot}

\begin{document}

\section*{PhysRevD.62.044034 equation (18)}

\begin{cadabra}
   from shared import *
   import cdblib

   jsonfile = 'eqtn18.json'
   cdblib.create (jsonfile)

   # --------------------------------------------------------------------------
   # RBar pt.1 = split into two terms

   defGammaBar := GammaBar^{a}_{b c} ->
                  (1/2) gBar^{a e} (   \partial_{b}{gBar_{e c}}
                                     + \partial_{c}{gBar_{b e}}
                                     - \partial_{e}{gBar_{b c}}).

   defRiemBar := RBar^{a}_{b c d} ->
                 \partial_{c}{GammaBar^{a}_{b d}} + GammaBar^{a}_{e c} GammaBar^{e}_{b d}
               - \partial_{d}{GammaBar^{a}_{b c}} - GammaBar^{a}_{e d} GammaBar^{e}_{b c}.

   defRBar := RBar_{a b} -> RBar^{c}_{a c b}.

   RBar := RBar_{a b}.                                                  # cdb(eq18.000,RBar)

   substitute   (RBar, defRBar)                                         # cdb(eq18.001,RBar)
   substitute   (RBar, defRiemBar)                                      # cdb(eq18.002,RBar)
   substitute   (RBar, $GammaBar^{a}_{b a} -> 0$)                       # cdb(eq18.003,RBar)  # follows from det g = 1
   canonicalise (RBar)
\end{cadabra}

\clearpage

\begin{dgroup*}[spread=5pt]
   \begin{dmath*}
      \cdb{eq18.000}
         = \Cdb*{eq18.001}
         = \Cdb*{eq18.002}
         = \Cdb*{eq18.003}
   \end{dmath*}
\end{dgroup*}

From here the computations will be splt into two threads, one for each of the two terms in the above result.

\clearpage

\begin{cadabra}
   # --------------------------------------------------------------------------
   # get tmpA & tmpB from RBar

   GammaBar^{a}_{b c}::Weight(label=numG).
   \partial{#}::WeightInherit(label=all, type=multiplicative).

   tmpA := @(RBar).                                                     # cdb(tmp18.101,tmpA)
   keep_weight (tmpA, $numG=1$)                                         # cdb(tmp18.102,tmpA)  # the derivative terms

   tmpB := @(RBar).
   keep_weight (tmpB, $numG=2$)                                         # cdb(tmp18.103,tmpB)  # the quadrtaic terms
\end{cadabra}

\begin{dgroup*}
   \begin{dmath*} \cdb*{tmp18.101} \end{dmath*}
   \begin{dmath*} \cdb*{tmp18.102} \end{dmath*}
   \begin{dmath*} \cdb*{tmp18.103} \end{dmath*}
\end{dgroup*}

\clearpage

\begin{cadabra}
   # --------------------------------------------------------------------------
   # tmpA pt. 1

   substitute   (tmpA, defGammaBar)                                     # cdb(tmp18.201,tmpA)
   distribute   (tmpA)                                                  # cdb(tmp18.202,tmpA)
   product_rule (tmpA)                                                  # cdb(tmp18.203,tmpA)
   substitute   (tmpA, $\partial_{a}{gBar^{a b}} -> - GammaBar^{b}$)    # cdb(tmp18.204,tmpA)
\end{cadabra}

\begin{dgroup*}[spread=5pt]
   \begin{dmath*}
      \cdb{tmp18.102}
         = \Cdb*{tmp18.201}
         = \Cdb*{tmp18.202}
         = \Cdb*{tmp18.203}
         = \Cdb*{tmp18.204}
   \end{dmath*}
\end{dgroup*}

Notice that this result contains two terms containg second derivatives of $\bar g_{ij}$.
This pair of terms will now be replaced with an expression built from the first derivatives
of $\Gamma^{i}$.

\clearpage

\begin{cadabra}
   # --------------------------------------------------------------------------
   # tmpC

   defGi := GammaBar^{i} -> - \partial_{j}{gBar^{i j}}.

   # lower the indices on gBar^{b c}

   defLowerIndices := \partial_{a}{gBar^{b c}} -> - gBar^{i b} gBar^{j c} \partial_{a}{gBar_{i j}}.

   substitute (defGi, defLowerIndices)

   tmpC := gBar_{a i} \partial_{b}{GammaBar^{i}}
         + gBar_{b i} \partial_{a}{GammaBar^{i}}.                       # cdb(tmp18.301,tmpC)

   saveC := @(tmpC).

   substitute   (tmpC, defGi)                                           # cdb(tmp18.302,tmpC)
   product_rule (tmpC)                                                  # cdb(tmp18.303,tmpC)
   distribute   (tmpC)                                                  # cdb(tmp18.304,tmpC)
   canonicalise (tmpC)                                                  # cdb(tmp18.305,tmpC)
   substitute   (tmpC, $gBar_{a b} gBar^{b c} -> gBar_{a}^{c}$)         # cdb(tmp18.306,tmpC)
   eliminate_kronecker (tmpC)                                           # cdb(tmp18.307,tmpC)

   # foo is the target expression to be moved to the lhs

   foo := gBar^{i j} \partial_{a i}{gBar_{b j}}
        + gBar^{i j} \partial_{b i}{gBar_{a j}} -> X_{a b}.

   # bah helps when rebuilding the equation

   bah := X_{a b} ->
          gBar^{i j} \partial_{a i}{gBar_{b j}}
        + gBar^{i j} \partial_{b i}{gBar_{a j}}.

   substitute (tmpC, foo)                                               # cdb(tmp18.308,tmpC)

   # |\clearpage|
   # rearrange to move the target to the lhs

   tmpE := @(tmpC).
   tmpF := @(tmpC).

   X_{a b}::Weight(label=numX).

   # get the two pieces of the equation
   keep_weight (tmpE, $numX=0$)                                         # cdb(tmp18.309,tmpE)
   keep_weight (tmpF, $numX=1$)                                         # cdb(tmp18.310,tmpF)

   substitute  (tmpF, bah)                                              # cdb(tmp18.311,tmpF)

   # now rebuild with terms reorderd
   tmpG := @(saveC) - @(tmpE).                                          # cdb(tmp18.312,tmpG)

   defTmpSub := @(tmpF) -> @(tmpG).                                     # cdb(tmp18.313,defTmpSub)
\end{cadabra}

\clearpage

\begin{dgroup*}[spread=5pt]
   \begin{dmath*}
      \cdb{tmp18.301}
         = \Cdb*{tmp18.302}
         = \Cdb*{tmp18.303}
         = \Cdb*[\hskip2cm\hfill]{tmp18.304}
         = \Cdb*[\hskip2cm\hfill]{tmp18.305}
         = \Cdb*{tmp18.306}
         = \Cdb*{tmp18.307}
         = \Cdb*{tmp18.308}
   \end{dmath*}
\end{dgroup*}

\begin{dgroup*}[spread=5pt]
   \begin{dmath*}
      \cdb{tmp18.311}
         = \Cdb*{tmp18.310}
         = \Cdb*{tmp18.312}
   \end{dmath*}
\end{dgroup*}

This result will now be applied to the earlier equation {\tt (tmp18.204)}.

\clearpage

\begin{cadabra}
   # --------------------------------------------------------------------------
   # tmpA pt.2 eliminate second partial derivatives of gBar

   canonicalise   (tmpA)                                                # cdb(tmp18.401,tmpA)

   substitute     (tmpA, defTmpSub)                                     # cdb(tmp18.402,tmpA)
   tmpA = product_sort (tmpA)
   rename_dummies (tmpA)
   canonicalise   (tmpA)                                                # cdb(tmp18.403,tmpA)

   foo := gBar^{d e} \partial_{c}{gBar_{e f}} -> - gBar_{e f} \partial_{c}{gBar^{d e}}.
   bah := \partial_{d}{gBar^{d f}} -> - GammaBar^{f}.

   substitute     (tmpA, foo)                                           # cdb(tmp18.404,tmpA)
   substitute     (tmpA, bah)                                           # cdb(tmp18.405,tmpA)

   foo := gBar_{e f} \partial_{a}{gBar^{c f}}  -> - \partial_{a}{gBar_{e f}} gBar^{c f}.

   substitute     (tmpA, foo)                                           # cdb(tmp18.406,tmpA)

   foo := gBar_{b d} gBar^{d e}  -> gBar_{b}^{e}.

   substitute     (tmpA, foo)                                           # cdb(tmp18.407,tmpA)
   eliminate_kronecker (tmpA)                                           # cdb(tmp18.408,tmpA)
   tmpA = product_sort (tmpA)
   rename_dummies (tmpA)
   canonicalise   (tmpA)                                                # cdb(tmp18.409,tmpA)
\end{cadabra}

\clearpage

\begin{dgroup*}[spread=5pt]
   \begin{dmath*}
      \cdb{tmp18.102}
         = \Cdb*{tmp18.401}
         = \Cdb*[\hskip2cm\hfill]{tmp18.402}
         = \Cdb*[\hskip2cm\hfill]{tmp18.403}
         = \Cdb*[\hskip2cm\hfill]{tmp18.404}
         = \Cdb*[\hskip2cm\hfill]{tmp18.405}
         = \Cdb*[\hskip2cm\hfill]{tmp18.406}
         = \Cdb*[\hskip2.5cm\hfill]{tmp18.407}
         = \Cdb*[\hskip2.5cm\hfill]{tmp18.408}
         = \Cdb*{tmp18.409}
   \end{dmath*}
\end{dgroup*}

\clearpage

\begin{cadabra}
   # --------------------------------------------------------------------------
   # tmpB

   substitute     (tmpB, defGammaBar)                                   # cdb(tmp18.501,tmpB)
   distribute     (tmpB)                                                # cdb(tmp18.502,tmpB)
   tmpB = product_sort (tmpB)                                           # cdb(tmp18.503,tmpB)
   rename_dummies (tmpB)                                                # cdb(tmp18.504,tmpB)
   canonicalise   (tmpB)                                                # cdb(tmp18.505,tmpB)
\end{cadabra}

\begin{dgroup*}[spread=5pt]
   \begin{dmath*}
      \cdb{tmp18.103}
         = \Cdb*{tmp18.501}
         = \Cdb*{tmp18.502}
         = \Cdb*{tmp18.503}
         = \Cdb*{tmp18.504}
         = \Cdb*{tmp18.505}
   \end{dmath*}
\end{dgroup*}

\clearpage

\begin{cadabra}
   # --------------------------------------------------------------------------
   # RBar pt.2 = Rebuild Rab from tmpA and tmpB

   RBar := @(tmpA) + @(tmpB).

   canonicalise   (RBar)                                                # cdb(eq18.601,RBar)

   foo := \partial_{a}{gBar^{c d}} -> - gBar^{c i} gBar^{d j} \partial_{a}{gBar_{i j}}.

   substitute     (RBar, foo)
   distribute     (RBar)
   RBar = product_sort (RBar)
   rename_dummies (RBar)
   canonicalise   (RBar)                                               # cdb(eq18.602,RBar)

   foo := \partial_{a}{gBar_{b c}} -> GammaBar_{b c a} + GammaBar_{c b a}.

   substitute     (RBar, foo)                                          # cdb(eq18.603,RBar)
   distribute     (RBar)
   RBar = product_sort (RBar)
   rename_dummies (RBar)
   canonicalise   (RBar)                                               # cdb(eq18.604,RBar)

   foo := GammaBar_{d e f} gBar^{d e} -> 0.

   substitute     (RBar, foo)                                          # cdb(eq18.605,RBar)

   defRab := RBar_{a b} -> @(RBar).
\end{cadabra}

\clearpage

\begin{dgroup*}[spread=5pt]
   \begin{dmath*}
      \cdb{eq18.000}
         = \Cdb*{eq18.003}
         = \Cdb*[\hskip2cm\hfill]{eq18.601}
         = \Cdb*{eq18.602}
         = \Cdb*{eq18.603}
         = \Cdb*{eq18.604}
         = \Cdb*{eq18.605}
   \end{dmath*}
\end{dgroup*}

\clearpage

\begin{cadabra}
   # --------------------------------------------------------------------------
   # Check against prd62.

   foo := @(RBar).                                      # cdb(eq18.lcb,foo)
   bah  = cdblib.get('prd62.eq18.rhs','prd62.json')     # cdb(eq18.prd,bah)

   diff := @(foo) - @(bah).

   distribute     (diff)
   diff = product_sort (diff)
   rename_dummies (diff)
   map_sympy      (diff, "simplify")
   canonicalise   (diff)                                # cdb(eq18.chk,diff)
\end{cadabra}

% \clearpage

\begin{dgroup*}
   \begin{dmath*} \cdb*{eq18.lcb} \end{dmath*}
   \begin{dmath*} \cdb*{eq18.prd} \end{dmath*}
   \begin{dmath*} \cdb*{eq18.chk} \end{dmath*}
\end{dgroup*}

\end{document}
