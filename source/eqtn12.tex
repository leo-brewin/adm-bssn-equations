\documentclass[12pt]{cdblatex}
\usepackage{eqtns}
\usepackage{fancyhdr}
\usepackage{footer}

\begin{document}

\section*{PhysRevD.62.044034 equation (12)}

\begin{cadabra}
   from shared import *
   import cdblib

   jsonfile = 'eqtn12.json'
   cdblib.create (jsonfile)

   DgijDt = cdblib.get ('adm.DgijDt','adm.json')
   DKijDt = cdblib.get ('adm.DKijDt','adm.json')

   DphiDt = cdblib.get ('DphiDt','eqtn10.json')
   DKDt = cdblib.get ('DKDt','eqtn11.json')

   # --------------------------------------------------------------------------
   ABar2A := ABar_{i j} -> \exp(-4\phi) A_{i j}.         # prd62 eqn 08
   A2ABar := A_{i j} -> \exp(4\phi) ABar_{i j}.          # prd62 eqn 08

   Aij    := A_{i j} -> K_{i j} - (1/3) g_{i j} trK.     # prd62 eqn 07
   Kij    := K_{i j} -> A_{i j} + (1/3) g_{i j} trK.     # prd62 eqn 07

   gginv := {g_{i a} g^{a j} -> g_{i}^{j},
             g_{i a} g^{j a} -> g_{i}^{j}}.

   ABarUp := ABar_{i j} g^{j k} -> \exp(-4\phi) ABar_{i}^{k}.

   ABardotABar := ABar_{i j} ABar^{i j} ->
                  (K_{i j}-(1/3)g_{i j} trK) (K^{i j}-(1/3)g^{i j} trK).

   trg := g_{i j} g^{i j} -> 3.

   trK := {K_{i j} g^{i j} -> trK,
           K^{i j} g_{i j} -> trK}.

   Ham := trK**2 -> K_{i j} K^{i j} - g^{i j} R_{i j}.

   # --------------------------------------------------------------------------
   # dABarij/dt

   dotABarij := \partial_{t}{ABar_{i j}}.         # cdb (eq12.101,dotABarij)

   substitute     (dotABarij, ABar2A)             # cdb (eq12.102,dotABarij)
   product_rule   (dotABarij)                     # cdb (eq12.103,dotABarij)
   map_sympy      (dotABarij, "simplify")         # cdb (eq12.104,dotABarij)
   substitute     (dotABarij, DphiDt)             # cdb (eq12.105,dotABarij)
   substitute     (dotABarij, Aij)                # cdb (eq12.106,dotABarij)
   distribute     (dotABarij)                     # cdb (eq12.107,dotABarij)
   substitute     (dotABarij, DKijDt)             # cdb (eq12.108,dotABarij)
   product_rule   (dotABarij)                     # cdb (eq12.109,dotABarij)
   distribute     (dotABarij)                     # cdb (eq12.110,dotABarij)
   substitute     (dotABarij, DKDt)               # cdb (eq12.111,dotABarij)
   substitute     (dotABarij, DgijDt)             # cdb (eq12.112,dotABarij)
   distribute     (dotABarij)                     # cdb (eq12.113,dotABarij)
   substitute     (dotABarij, Kij)                # cdb (eq12.114,dotABarij)
   distribute     (dotABarij)                     # cdb (eq12.115,dotABarij)
   substitute     (dotABarij, gginv)              # cdb (eq12.116,dotABarij)
   eliminate_kronecker (dotABarij)                # cdb (eq12.117,dotABarij)
   substitute     (dotABarij, A2ABar)             # cdb (eq12.118,dotABarij)
   canonicalise   (dotABarij)                     # cdb (eq12.119,dotABarij)
   substitute     (dotABarij, ABardotABar)        # cdb (eq12.120,dotABarij)
   distribute     (dotABarij)                     # cdb (eq12.121,dotABarij)
   substitute     (dotABarij, trg)                # cdb (eq12.122,dotABarij)
   substitute     (dotABarij, trK)                # cdb (eq12.123,dotABarij)
   map_sympy      (dotABarij, "simplify")         # cdb (eq12.124,dotABarij)
   substitute     (dotABarij, Ham)                # cdb (eq12.125,dotABarij)
   distribute     (dotABarij)                     # cdb (eq12.126,dotABarij)
   dotABarij = product_sort (dotABarij)           # cdb (eq12.127,dotABarij)
   substitute     (dotABarij, ABarUp)             # cdb (eq12.128,dotABarij)
   map_sympy      (dotABarij, "simplify")         # cdb (eq12.129,dotABarij)
   factor_out     (dotABarij,$\exp(-4\phi)$)      # cdb (eq12.130,dotABarij)

   DABarijDt := \partial_{t}{ABar_{ij}} -> @(dotABarij).

   cdblib.put ('DABarijDt',DABarijDt,jsonfile)
\end{cadabra}

\clearpage

\begin{dgroup*}[spread=5pt]
   \begin{dmath*}
      \cdb{eq12.101}
         = \Cdb*{eq12.102}
         = \Cdb*{eq12.103}
         = \Cdb*{eq12.104}
         = \Cdb*{eq12.105}
         = \Cdb*{eq12.106}
         = \Cdb*{eq12.107}
         = \Cdb*[\hskip2cm\hfill]{eq12.108}
         = \Cdb*{eq12.109}
         = \Cdb*{eq12.110}
         = \Cdb*{eq12.111}
         = \Cdb*{eq12.112}
   \end{dmath*}
\end{dgroup*}

\clearpage

\begin{dgroup*}[spread=5pt]
   \begin{dmath*}
      \cdb{eq12.101}
         = \Cdb*[\hskip2cm\hfill]{eq12.113}
         = \Cdb*{eq12.114}
         = \Cdb*{eq12.115}
         = \Cdb*{eq12.116}
         = \Cdb*{eq12.117}
   \end{dmath*}
\end{dgroup*}

\clearpage

\begin{dgroup*}[spread=5pt]
   \begin{dmath*}
      \cdb{eq12.101}
         = \Cdb*{eq12.118}
         = \Cdb*{eq12.119}
         = \Cdb*{eq12.120}
         = \Cdb*{eq12.121}
   \end{dmath*}
\end{dgroup*}

\clearpage

\begin{dgroup*}[spread=5pt]
   \begin{dmath*}
      \cdb{eq12.101}
         = \Cdb*{eq12.122}
         = \Cdb*{eq12.123}
         = \Cdb*{eq12.124}
         = \Cdb*{eq12.125}
         = \Cdb*{eq12.126}
         = \Cdb*[\hskip2cm\hfill]{eq12.127}
   \end{dmath*}
\end{dgroup*}

\clearpage

\begin{dgroup*}[spread=5pt]
   \begin{dmath*}
      \cdb{eq12.101}
         = \Cdb*[\hskip2cm\hfill]{eq12.128}
         = \Cdb*{eq12.129}
         = \Cdb*{eq12.130}
   \end{dmath*}
\end{dgroup*}

\clearpage

\begin{cadabra}
   # --------------------------------------------------------------------------
   # Check against prd62.

   foo := @(dotABarij).                                 # cdb(eq12.lcb,foo)
   bah  = cdblib.get('prd62.eq12.rhs','prd62.json')     # cdb(eq12.prd,bah)

   diff := @(foo) - @(bah).

   foo := ABar_{a}^{b} -> gBar^{b c} ABar_{a c}.
   bah := ABar^{a}_{b} -> gBar^{a c} ABar_{c b}.

   substitute     (diff, foo)
   substitute     (diff, bah)
   distribute     (diff)
   diff = product_sort (diff)
   rename_dummies (diff)
   map_sympy      (diff, "simplify")
   canonicalise   (diff)                                # cdb(eq12.chk,diff)
\end{cadabra}

% \clearpage

\begin{dgroup*}
   \begin{dmath*} \cdb*{eq12.lcb} \end{dmath*}
   \begin{dmath*} \cdb*{eq12.prd} \end{dmath*}
   \begin{dmath*} \cdb*{eq12.chk} \end{dmath*}
\end{dgroup*}

\end{document}
